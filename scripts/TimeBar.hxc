import funkin.play.PlayState;
import funkin.modding.module.Module;
import funkin.Paths;
import flixel.FlxSprite;
import funkin.Conductor;
import flixel.FlxG;
import flixel.text.FlxText;
import flixel.ui.FlxBar;
import flixel.util.FlxStringUtil;
import flixel.text.FlxTextBorderStyle;
import funkin.Preferences;
import openfl.utils.ByteArrayData;
import funkin.save.Save;

class TimeBar extends Module {

    var songPercentage:Float;

    var timeBarBG:FlxSprite;
    var timeBar:FlxBar;
    var timeBarEmptyColour = 0xFF000000;
    var timeBarFillColour = 0xFFFFFFFF;

    var timeText:FlxText;
    var timeTextColour = 0xFFFFFFFF;
    var timeTextOutlineColour = 0xFF000000;
    var playerStrumline;
    

    var iconColour;
    var byteReader:ByteArrayData;

    var playerID:Int = 0;
    var opponentID:Int = 0;

    function new (){
        super("timeBarMod", 10, {state:PlayState});
    }

    override function onStateChangeEnd(event)
    {
        super.onStateChangeEnd(event);

        playerStrumline = PlayState.instance.playerStrumline;
   
        switch (Save.instance.modOptions["TimeBarColour"])
        {
            case "Opponent Icon":
                timeBarFillColour = IconColourFinder.findIconColour(PlayState.instance.iconP2);
            case "Player Icon":
                timeBarFillColour = IconColourFinder.findIconColour(PlayState.instance.iconP1);
            default:
                timeBarFillColour = 0xFFFFFFFF;
        }
    
    
        if (Save.instance.modOptions["TimeBar"] != "Off")
        {
            initTimebar();
            setTimeBar();
            showText();
            resetVariables();
        }

     

       
        
    }
 
    override function onUpdate(event)
    {
        super.onUpdate(event);            
        if (FlxG.sound.music != null)
        {
            songPercentage = (Conductor.instance.songPosition / FlxG.sound.music.length);
        }

        if (timeBarBG != null)
        {
            timeBar.value = songPercentage;
        }
        if (timeText != null)
        {
            updateTimeText();
        }

        if (timeBar != null)
		{
	        switch (Save.instance.modOptions["TimeBarColour"])
            {
                case "Opponent Icon":
                    if (opponentID != PlayState.instance.iconP2.characterId)
				    {
				    	opponentID = PlayState.instance.iconP2.characterId;

				    	timeBarFillColour = IconColourFinder.findIconColour(PlayState.instance.iconP2);

				    	timeBar.createFilledBar(timeBarEmptyColour, timeBarFillColour);
                        timeBar.updateBar();
				    }
                case "Player Icon":
                    if (playerID != PlayState.instance.iconP1.characterId)
				    {
				    	playerID = PlayState.instance.iconP1.characterId;

				    	timeBarFillColour = IconColourFinder.findIconColour(PlayState.instance.iconP1);

				    	timeBar.createFilledBar(timeBarEmptyColour, timeBarFillColour);
                        timeBar.updateBar();
				    }
            }
		}
        
    }

    // ------ Custom Functions

    function initTimebar()
    {
        timeBarBG = new FlxSprite();
        switch (Save.instance.modOptions["TimeBar"])
        {
            case "On":
                timeBarBG.loadGraphic(Paths.image("timebar"));
            case "Wide":
                timeBarBG.loadGraphic(Paths.image("widetimebar"));
        }
        timeBarBG.camera = PlayState.instance.camHUD;
        timeBarBG.zIndex = 1001;

        timeBar = new FlxBar(100, 0, "LEFT_TO_RIGHT", timeBarBG.width - 8, timeBarBG.height - 8, this, "songPercentage", 0, 1);
        timeBar.createFilledBar(timeBarEmptyColour, timeBarFillColour);
        timeBar.camera = PlayState.instance.camHUD;
        timeBar.zIndex = 1002;

        PlayState.instance.add(timeBarBG);
        PlayState.instance.add(timeBar);
    }

    function setTimeBar()
    {
        switch (Save.instance.modOptions["TimeBarPos"])
        {
            case "Right":
                verticalTimeBar();
            case "Left":
                verticalTimeBar();
            case "Strumline":
                timeBarBG.setPosition(playerStrumline.x + (playerStrumline.width / 2) - (timeBarBG.width / 2), (timeBarBG.height / 2) - 4);

                if (!Save.instance.modOptions["ForceTimeBarPos"])
                {
                    if (Preferences.downscroll || Preferences.controlsScheme == "Arrows")
                    {
                        timeBarBG.y = (FlxG.height - timeBarBG.height) - 4;
                    }
                }
          
                timeBar.setPosition(timeBarBG.x + 4, timeBarBG.y + 4);
            case "Top":
                defaultTimeBarPosition();
            case "Middle":
                middlePosition();
            default:
                defaultTimeBarPosition();
        }
    }

    function defaultTimeBarPosition()
    {
        if (playerStrumline.x == (FlxG.width / 2) - (playerStrumline.width / 2))
        {
            middlePosition();
            return;
        }
        timeBarBG.setPosition((FlxG.width / 2 - timeBar.width / 2) - 50 , 10);
        if (!Save.instance.modOptions["ForceTimeBarPos"])
        {
            if (Preferences.downscroll || Preferences.controlsScheme == "Arrows")
            {
                timeBarBG.y = FlxG.height - (timeBarBG.height) - (timeBarBG.height / 2);
            }
        }


        timeBar.setPosition(timeBarBG.x + 4, timeBarBG.y + 4);

    }
    
    function middlePosition()
    {
        timeBarBG.setPosition((FlxG.width / 2) - (timeBarBG.width / 2), timeBarBG.height / 2);

        if (!Save.instance.modOptions["ForceTimeBarPos"])
        {
            if (Preferences.downscroll || Preferences.controlsScheme == "Arrows")
            {
                timeBarBG.y = FlxG.height - (timeBarBG.height) - (timeBarBG.height / 2);
            }
        }
        timeBar.setPosition(timeBarBG.x + 4, timeBarBG.y + 4);
    }


    function showText()
    {
        timeText = new FlxText();
        timeText.text = "0:00";

        timeText.setFormat(Paths.font("vcr.ttf"), 32, timeTextColour, "center", FlxTextBorderStyle.OUTLINE, timeTextOutlineColour);
        timeText.borderSize = 2;
        timeText.setPosition(timeBarBG.x + (timeBarBG.width / 2) - (timeText.width / 2), timeBarBG.y + (timeBarBG.height / 2) - (timeText.height / 2));
        timeText.camera = PlayState.instance.camHUD;
        timeText.zIndex = 1003;

        PlayState.instance.add(timeText);
        
    }

    function updateTimeText()
    {
        switch (Save.instance.modOptions["TimeBarMode"])
        {
            case "None":
                timeText.text = "";
            case "Time Elapsed":
                if (Conductor.instance.songPosition >= 0)
                {
                    timeText.text = FlxStringUtil.formatTime(Conductor.instance.songPosition / 1000, false);
                }
            case "Time Left":
                if (FlxG.sound.music != null)
                {
                    timeText.text = "" + FlxStringUtil.formatTime((FlxG.sound.music.length - FlxG.sound.music.time) / 1000, false);
                }
            case "Song Name":
                timeText.text = PlayState.instance.currentSong.songName;
            case "Song Name + Difficulty":
                timeText.text = PlayState.instance.currentSong.songName + " - " + FlxStringUtil.toTitleCase(PlayState.instance.currentDifficulty);

        }

                
        switch (Save.instance.modOptions["TimeTextPos"])
        {
            case "Default":
                if (Save.instance.modOptions["TimeBarPos"] == "Right")
                {
                    if (Preferences.downscroll || Preferences.controlsScheme == "Arrows")
                    {
                        timeText.setPosition(timeBarBG.x - timeText.width, timeBarBG.y - timeText.height);
                    }
                    else
                    {
                        timeText.setPosition(timeBarBG.x - timeText.width, timeBarBG.y + timeBarBG.width);
                    }
                }
                else if (Save.instance.modOptions["TimeBarPos"] == "Left")
                {
                    timeText.setPosition(timeBarBG.x - timeBarBG.height, timeBarBG.y + timeBarBG.width);

                    if (Preferences.downscroll || Preferences.controlsScheme == "Arrows")
                    {
                        timeText.y = timeBarBG.y - timeText.height;
                    }
            
                }
                else
                {
                    timeText.setPosition(timeBarBG.x + (timeBarBG.width / 2) - (timeText.width / 2), timeBarBG.y + (timeBarBG.height / 2) - (timeText.height / 2));
                }
            case "Strumline":
                if (Save.instance.modOptions["TimeBarPos"] != "Strumline")
                {
                    if (Preferences.downscroll || Preferences.controlsScheme == "Arrows")
                    {
                        timeText.setPosition(playerStrumline.x + (playerStrumline.width / 2) - (timeText.width / 2), (FlxG.height - timeText.height) - 4); //(playerStrumline.y + playerStrumline.height - (timeText.height / 2)) + 4)
                    }
                    else
                    {
                        timeText.setPosition(playerStrumline.x + (playerStrumline.width / 2) - (timeText.width / 2), (timeText.height / 2) - 4);
                    }
                }
            case "Force Top":
                timeText.setPosition((FlxG.width / 2) - (timeText.width / 2), (timeText.height / 2) - 4);

        }
   
    }

    function resetVariables()
    {
        if (timeBar != null)
        {
            timeBar.value = 0;
        }
        songPercentage = 0;
    }
    

    function verticalTimeBar()
    {

        if (Save.instance.modOptions["TimeBarPos"] == "Right")
        {
            timeBarBG.setPosition(FlxG.width - (timeBarBG.height / 2), (FlxG.height / 2) - (timeBarBG.width / 2));
        }
        else if (Save.instance.modOptions["TimeBarPos"] == "Left")
        {
            timeBarBG.setPosition(timeBarBG.height * 1.5, (FlxG.height / 2) - (timeBarBG.width / 2));
        }
        timeBarBG.origin.set(0, 0);
        timeBarBG.angle = 90;

        timeBar.setPosition(timeBarBG.x - 4, timeBarBG.y + 4);
        timeBar.origin.set(0, 0);
        timeBar.angle = 90;
        timeBar.flipX = true;

    

    }
}
